private class PingSuper extends Chubgraph
{
    LPF lpf => outlet;
    
    220 => float freq;
    1::second => dur approxFadeDur;
    
    s();
    
    fun void s()
    {
        lpf.freq(freq);
        lpf.Q(freq * (approxFadeDur/second));
    }
    
    
    fun void set(dur fadeDur) { fadeDur => approxFadeDur; s(); }
    fun void set(float myFreq) { myFreq => freq; s(); }
    fun void set(dur fadeDur, float myFreq) { fadeDur => approxFadeDur; myFreq => freq; s(); }
    
    fun void hit(float Hit) { }
    
    // for Ping3.
    1 => int reps;
    1 => float fade; // gotta be between 1 and 0 (maybe 1 and -1)
    samp => dur delay; // between successive impulses.
    
    // for Ping4. (for Ping4B too.)
    .1::second => dur attack; .1::second => dur decay; 
    fun void set(dur Att, dur Dec){ Att => attack; Dec => decay; }
    
    // for Ping4B, and Ping2.
    .5 => float sustain; .1::second => dur release;
    fun void set(dur att, dur dec, float sus, dur rel) { }
    0 => int holding;
    fun void hold(int Holding) { (Holding != 0) => holding; }
    Event stop;
    fun void stopBowing() { stop.broadcast(); }
    
    fun dur freqPeriod() { return second / freq; }
    // this does absolutely nothing, except in the case of Ping3. Why the hell did I type all this out
    fun void setR(int rep){ rep => reps; } 
    fun void setF(float fad){ fad => fade; } 
    fun void setD(dur dela){ dela => delay; }
    fun void setShwip(int rep, float fad, dur dela){ setR(rep), setF(fad), setD(dela); }
}

private class Ping extends PingSuper
{
    Impulse imp => lpf;
    fun void hit(float Hit) { imp.next(Hit); }
}
private class PingB extends Ping // all this does is add two sine waves of the same frequency, more or less.
{
    .5 => float anti;
    
    fun void hit(float Hit) { spork ~ hitComponent(Hit); }
    fun void hitComponent(float Hit)
    { 
        imp.next(Hit);
        if(anti>0) anti::freqPeriod() => now;
        else samp => now;
        imp.next(-Hit); 
    }
}
private class Ping2 extends PingSuper
{
    Step constant => ADSR env => lpf;
    constant.next(1);
    env.set(samp, samp, .5, samp);
    
    0::second => dur susTime; // whatever the effects of changing this are, they won't affect already-sounding notes, I think.
    
    fun void set(dur att, dur dec, float sus, dur rel) { env.set(att, dec, sus, rel); }
    
    fun void hit(float Hit, dur length)
    {
        env.target(Hit);
        spork ~ hitComponent();
    }
    fun void hitComponent()
    {
        env.attackTime() + env.decayTime() + susTime => now;
        env.keyOff(1);
    }
}
private class Ping3 extends PingSuper
{
    Impulse imp => lpf;
    
    fun void hit(float Hit){ spork ~ hitComponent(Hit); }
    fun void hitComponent(float Hit)
    {
        for(0 => int repCount; repCount < reps; repCount++)
        {
            imp.next(Hit*Math.pow(fade,repCount));
            delay => now;
        }
    }
}
private class PingDelay extends PingSuper
{
    .9 => fade;
    100::samp => delay;
    
    Impulse imp => Delay d => lpf;
    d => Gain g => d; g.gain(fade);
    
    
    second/delay => float delayFreq;
    setupDelay();
    
    fun void setupDelay() { d.delay(second / delayFreq); d.delay() => delay; }
    fun void setDelayFreq(float myFreq) { myFreq => delayFreq; setupDelay(); }
    
    fun void setF(float fad){ fad => fade; g.gain(fade); }
    fun void setD(dur dela){ dela => delay; delay => d.delay; second/delay => float delayFreq;}

    fun void hit(float Hit) { imp.next(Hit); }
}

private class PingDelayPitch extends PingDelay // this seems broken at the moment.
{
    1 => float ratio;
    ratio*freq => delayFreq;
    fun void setupDelay() { ratio*freq => delayFreq; d.delay(second / delayFreq); d.delay() => delay; }
    fun void setDelayRatio(float myRatio) { myRatio => ratio; setupDelay(); }
    
    fun void set(float myFreq) { myFreq => freq; setupDelay(); s(); }
    fun void set(dur fadeDur, float myFreq) { fadeDur => approxFadeDur; myFreq => freq; setupDelay(); s(); }

}
private class Ping4 extends PingSuper
{
    Noise noise => Envelope env => lpf;
    
    fun void hit(float Hit){ spork ~ hitComponent(Hit); }
    fun void hitComponent(float Hit)
    { 
        env.target(Hit);
        env.duration(attack);
        attack => now;
        env.target(0);
        env.duration(decay);
    }
}
private class Ping4B extends PingSuper
{
    Noise noise => ADSR env => lpf;
    
    .01::second => attack;
    .01::second => decay;
    .5 => sustain;
    .01::second => release;
    
    fun void setup(){ env.set(attack, decay, sustain, release); }
    
    fun void set(dur att, dur dec, float sus, dur rel) { att => attack; dec => decay; sus => sustain; rel => release; }
    
    fun void hit(float Hit){ setup(); spork ~ hitComponent(Hit); }
    fun void hitComponent(float Hit)
    { 
        env.target(Hit);
        attack => now; decay => now;
        if(holding) stop => now;
        env.target(0); env.duration(release);
    }
}
private class PingAdditive extends Chubgraph
{
    Gain g => outlet;
    
    1 => int num;
    Ping p[num];
    
    0.5 => float aVar;                 // first term of the geometric series determining gain.
    fun float a(float newA) { newA => aVar; setup(); return a(); }
    fun float a() { return aVar;     } // (please use these two functions for reference.) 
    fun float r() { return 1 - aVar; } // second term of said series divided by first term.
    
    
    
    220 => float freq;
    1::second => dur approxFadeDur;
    .5 => float fadeRatio;
    // if fadeRatio is .5, then
    // p[0] will last 1::approxFadeDur, 
    // p[1] will last .5::approxFadeDur,
    // p[2] will last .25::approxFadeDur, etc.
    // if fadeRatio is 1.5, then
    // p[0] will last 1::approxFadeDur,
    // p[1] will last 1.5::approxFadeDur,
    // p[2] will last 2.25::approxFadeDur, etc.
    
    setup();
    
    fun void setup()
    {
        for(0 => int pCount; pCount < p.size(); pCount++)
        {
            if(!p[pCount].isConnectedTo(g)) p[pCount] => g;
            
            a() * Math.pow(r(), pCount-(pCount == p.size()-1)) => p[pCount].gain; // wow, that's so much simpler.
            
            p[pCount].set(
                          Math.pow(fadeRatio, pCount)::approxFadeDur,
                          freq*(pCount+1)
            );
        }
    }
    fun void add(int howMany) // Be very careful as you go up. It seems different a()s have different behaviors, though they all ultimately approach painful snaps. 
    { // maybe just stick to 6 at most for now.
        repeat(howMany)
        {
            p << new Ping;
        }
        p.size() => num;
        setup();
    }
    fun void setFadeRatio(float fr) { fr => fadeRatio; setup(); }
    fun void set(dur fadeDur) { fadeDur => approxFadeDur; setup(); }
    fun void set(float myFreq) { myFreq => freq; setup(); }
    fun void set(dur fadeDur, float myFreq) { fadeDur => approxFadeDur; myFreq => freq; setup(); }
    fun void hit(float Hit)
    {
        for(0 => int pCount; pCount < p.size(); pCount++) p[pCount].hit(Hit);
    }
}
private class PingSuperAdditive extends Chubgraph
{
    Gain g => outlet;
    
    1 => int num;
    PingSuper @ p[num];
    
    0 => int allowSetup;
    
    0.5 => float aVar;                 // first term of the geometric series determining gain.
    fun float a(float newA) { newA => aVar; setup(); return a(); }
    fun float a() { return aVar;     } // (please use these two functions for reference.) 
    fun float r() { return 1 - aVar; } // second term of said series divided by first term.
    
    220 => float freq;
    1::second => dur approxFadeDur;
    .5 => float fadeRatio;
    // if fadeRatio is .5, then
    // p[0] will last 1::approxFadeDur, 
    // p[1] will last .5::approxFadeDur,
    // p[2] will last .25::approxFadeDur, etc.
    // if fadeRatio is 1.5, then
    // p[0] will last 1::approxFadeDur,
    // p[1] will last 1.5::approxFadeDur,
    // p[2] will last 2.25::approxFadeDur, etc.
    
    // for Ping4.
    .1::second => dur attack; .1::second => dur decay; .5 => float sustain; .1::second => dur release;
    
    setup();
    
    fun void allow() // this indicates you are done and that no null references remain in p.
    {
        1 => allowSetup;
        setup();
    }
    fun void setup() // this is for use by other class functions.
    {
        if(allowSetup)
        {
            for(0 => int pCount; pCount < p.size(); pCount++)
            {
                if(!p[pCount].isConnectedTo(g)) p[pCount] => g;
                
                a() * Math.pow(r(), pCount-(pCount == p.size()-1)) => p[pCount].gain; // wow, that's so much simpler.
                
                p[pCount].set(
                            Math.pow(fadeRatio, pCount)::approxFadeDur,
                            freq*(pCount+1)
                );
                
                attack => p[pCount].attack;
                decay => p[pCount].decay;
                sustain => p[pCount].sustain;
                release => p[pCount].release;
            }
        }
    }
    fun void add(int howMany) // don't do this after allow().
    { 
        p.size(p.size()+howMany);
        p.size() => num;
    }
    // for Ping4B.
    fun void hold(int Holding) { for(0 => int pCount; pCount < p.size(); pCount++) p[pCount].hold(Holding); }
    fun void stopBowing() { for(0 => int pCount; pCount < p.size(); pCount++) p[pCount].stopBowing(); }
    fun void set(dur att, dur dec, float sus, dur rel) { att => attack; dec => decay; sus => sustain; rel => release; setup(); } 
    
    fun void setFadeRatio(float fr) { fr => fadeRatio; setup(); }
    fun void set(dur fadeDur) { fadeDur => approxFadeDur; setup(); }
    fun void set(float myFreq) { myFreq => freq; setup(); }
    fun void set(dur fadeDur, float myFreq) { fadeDur => approxFadeDur; myFreq => freq; setup(); }
    fun void set(dur Att, dur Dec) { Att => attack; Dec => decay; setup();} // this is for Ping4.
    fun void hit(float Hit) // until you pick which descendent of PingSuper each one is, and turn startConnectingToG on, this will make no sound. 
    {
        for(0 => int pCount; pCount < p.size(); pCount++) p[pCount].hit(Hit);
    }
}
private class PingSuperAdditiveChoral extends PingSuperAdditive
{
    80 => float offsetNum; // effectively, offset is 80/79.
    fun float offset(){ return offsetNum * Math.pow(offsetNum-1,-1); }
    
    fun void setup()
    {
        if(allowSetup)
        {
            for(0 => int pCount; pCount < p.size(); pCount++)
            {
                if(!p[pCount].isConnectedTo(g)) p[pCount] => g;
                
                a() * Math.pow(r(), pCount-(pCount == p.size()-1)) => p[pCount].gain; // wow, that's so much simpler.
                
                p[pCount].set(
                            Math.pow(fadeRatio, pCount)::approxFadeDur,
                            forloopRandom2f(pCount, offset() * freq * (pCount+1), Math.pow(offset(),-1) * freq * (pCount+1))
                );
                
                attack => p[pCount].attack;
                decay => p[pCount].decay;
            }
        }
    }
}











// first, before anything else, a blast from the past:...
Gain safeG => Dyno safeD => Envelope fadeout => dac;

fadeout.value(1); fadeout.duration(9::second);
.5 => float maxAmp;
safeG.gain(maxAmp);
safeD.thresh(maxAmp);
safeD.slopeBelow(1.0);
safeD.slopeAbove(0.0);

PingSuperAdditive p => safeG;
p.add(9);
for(0 => int ppCount; ppCount < p.num; ppCount++)
{
    new Ping4B @=> p.p[ppCount];
}
p.setFadeRatio(.67); // .333 gets you horrible noise; .334 gets you sul tasto.
p.a(.5);
p.set(.07::second, .06::second, .8, .9::second); // sets ADSR of the noise being filtered.
p.set(.5::second, 440);                         // sets length, specifically of the sine tones that stay after the noise is done.

p.allow();
p.hit(1);

2::second => now;
p.stopBowing();

5::second => now;






fun float division(float num, float denom)
{
    return num * Math.pow(denom, -1);
}
fun int forloopMaybe(int count)
{ 
    maybe => int Maybe; 
    if(count == 0) return Maybe;
        else if(count == 1) return !Maybe;
            else if(count > 1) return maybe;
}
fun int forloopRandom2(int count, int floor, int cap)
{
    maybe => int Maybe;
    cap - floor => int difference;
    if(count == 0) return floor + Maybe*difference;
        else if(count == 1) return floor + (!Maybe)*difference;
            else if(count > 1) return Math.random2(floor, cap);
}
fun float forloopRandom2f(int count, float floor, float cap)
{
    maybe => int Maybe;
    cap - floor => float difference;
    if(count == 0) return floor + Maybe*difference;
        else if(count == 1) return floor + (!Maybe)*difference;
            else if(count > 1) return Math.random2f(floor, cap);
}







